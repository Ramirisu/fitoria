file(GLOB FITORIA_TESTS_SOURCE_FILES "*_test.cpp")

foreach(file ${FITORIA_TESTS_SOURCE_FILES})
  get_filename_component(file_name ${file} NAME)
  string(REPLACE ".cpp" "" target_name ${file_name})
  add_executable(${target_name} ${file})
  target_include_directories(${target_name} PRIVATE .)
  target_link_libraries(${target_name} PRIVATE fitoria doctest::doctest)
  if((CMAKE_CXX_COMPILER_ID MATCHES "MSVC")
     OR (CMAKE_CXX_COMPILER_FRONTEND_VARIANT MATCHES "MSVC"))
    target_compile_options(${target_name} PRIVATE /bigobj /W4 /WX)
  else()
    target_compile_options(${target_name} PRIVATE -Wall -Wextra -Werror
                                                  -pedantic -pedantic-errors)
    if(FITORIA_ENABLE_CODECOV)
      target_compile_options(${target_name} PRIVATE --coverage)
      target_link_libraries(${target_name} PRIVATE gcov)
    endif()
  endif()
  add_test(${target_name} ${target_name})
endforeach()
